# directories options
setopt AUTO_CD
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt PUSHD_SILENT
setopt PUSHD_TO_HOME

# history
HISTSIZE=9999
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_SAVE_NO_DUPS
setopt INC_APPEND_HISTORY

# I/O
unsetopt CLOBBER # so we can '>' to truncate existing file

# globbing
setopt EXTENDED_GLOB

# scripts
setopt MULTIOS # so we can: `$ echo 'foo' > file1 > file2`

# zle
bindkey -e
bindkey "^[^[[D" backward-word         # alt-left
bindkey "^[^[[C" forward-word          # alt-right
bindkey "^[^?" backward-kill-word      # alt-backspace
bindkey "^[[3~" delete-char            # delete
bindkey "^[3;5~" delete-char           # delete

# aliases
alias grep="grep --color=auto"
alias less="less -R"
alias l="ls -1A"
alias ls="ls -HFG"
alias ll="ls -lh"
alias la="ll -A"
alias sl="ls"
alias df="df -kh"
alias man="LC_ALL=C LANG=C man"

# safe
alias cp="cp -i"
alias ln="ln -i"
alias mv="mv -i"
alias rm="rm -i"

# prompt
setopt PROMPT_SUBST
prompt_status() {
  echo -n "%(?.%F{green}.%F{red})● %f"
}
prompt_user() {
  if [[ $UID -eq 0 ]]; then
    echo -n "%K{red}%F{white}root %k%f"
  fi
}
prompt_dir() {
  # colors for the prompt at https://framagit.org/mpo/dotfiles/blob/0fbc8e070793f060b90690fb99d526e8f94fd8ad/zsh/.zsh-color-names
  # part of shrink path stolen from https://github.com/robbyrussell/oh-my-zsh/blob/master/plugins/shrink-path/shrink-path.plugin.zsh
  echo -n "%F{green}"
  echo -n $(pwd | perl -pe "
   BEGIN {
      binmode STDIN,  ':encoding(UTF-8)';
      binmode STDOUT, ':encoding(UTF-8)';
    }; s|^$HOME|~|g; s|/([^/])[^/]*(?=/)|/\$1|g
  ")
  echo -n " %f"
}
prompt_git() {
  local repo git_status ref
  # repo=$(git rev-parse --git-dir 2>/dev/null)
  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref=$(git show-ref --head -s --abbrev |head -n1 2> /dev/null)
    echo -n "%F{magenta}⤞ ${ref/refs\/heads\//}"
    git_status=$(git status --porcelain 2> /dev/null | tail -n1)
    if [[ -n $git_status ]]; then
      echo -n "*"
    fi
    echo -n " %f"
  fi
}
PROMPT='
%{%f%b%k%}$(prompt_status)$(prompt_user)$(prompt_dir)$(prompt_git)\$ '
RPROMPT='%*'


# completions
setopt ALWAYS_TO_END # cursor to end of the word
setopt PATH_DIRS # search even if there is a slash (i.e. : useful for `bin/cons<tab>`)
setopt AUTO_MENU # when i smash <tab>, you have to show me the completions, FAST
setopt AUTO_PARAM_SLASH # if the completion is a dir, add the trailing slash
setopt AUTO_REMOVE_SLASH
setopt AUTO_LIST # automatically list choices on ambiguous completion.
unsetopt MENU_COMPLETE # do not pick automatically the first completion

fpath=($(brew --prefix)/share/zsh-completions $fpath)

autoload -Uz compinit
_comp_files=($HOME/.zcompdump(Nm-20)) # 20 hours of cache on completions
if (( $#_comp_files )); then
  compinit -i -C
else
  compinit -i
fi
unset _comp_files

zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' # case insensitive
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path $HOME/.zcompcache

if [[ -f $HOME/.github_token ]]; then
  export GITHUB_TOKEN=$(cat $HOME/.github_token)
fi

# fancy
alias szshrc='source ~/.zshrc'
alias h='history -iD'
alias fuck='sudo $(history -p \!\!)'
alias git=hub

export PATH="$PATH:`yarn global bin`"
